# idea for optimization: remove most functions and replace them with macros
# function calls seem to be a lot heavier than i thought

# Computer design parameters
#.const ram,              $0x0000
.const display,          $0x4000
.const _dev,             $0x8000
.const programMemory,    $0xC000

.const screenWidth,      $96
.const screenHeight,     $64
.const displaySize,      [screenWidth * screenHeight]
.const displayEnd,       [display + displaySize]

.const colorBlack,       $1
.const colorWhite,       $0
.const colorOrange,      $8
.const colorPink,        $9

.const colorMinGray,     $32
.const colorMaxGray,     $63

.const devbutton,        [_dev + 0]
.const devframe,         [_dev + 1]
.const devmidi,          [_dev + 2]
.const devrng,           [_dev + 0x3FFF] # TODO proper RNG, this is just an unconnected, floating cell

# button masks
.const buttonLeftUp,     [1 << 7]  # up arrow
.const buttonLeftDown,   [1 << 6]  # down arrow
.const buttonRightUp,    [1 << 3]  # top face
.const buttonRightDown,  [1 << 0]  # bottom face


# Program parameters
.const paddleSpeed,      $2
.const paddleHeight,     $8
.const paddlePadding,    $8
.const colorBackground,  colorBlack
.const colorPaddleLeft,  colorOrange
.const colorPaddleRight, colorPink
.const colorMidline,     [colorMinGray + (colorMaxGray - colorMinGray) * 75 / 100]


.const paddleLeftX,      paddlePadding
.const paddleRightX,     [screenWidth - paddlePadding]
.const paddleMaxY,       [screenHeight - paddleHeight]

.const fontWidth,        $3
.const fontHeight,       $5
.const fontArray,        [programMemory + fontArrayInProgramMemory]

.const scoreLeftX,       [screenWidth / 2 - 4 - fontWidth]
.const scoreRightX,      [screenWidth / 2 + 4]
.const scoreY,           $4


# Global variable addresses
.const paddleLeftY,      $0x10
.const paddleRightY,     $0x11
.const ballX,            $0x12
.const ballY,            $0x13
.const ballSpeedX,       $0x14
.const ballSpeedY,       $0x15
.const ballColor,        $0x16

.const scoreLeft,        $0x17
.const scoreRight,       $0x18

.const inputLeftDir,     $0x19
.const inputRightDir,    $0x1A




# --------------------------------------
@init_stack

#@call clearScreen, $0
@call resetBall, $0

ldc %b, [(screenHeight - paddlePadding) / 2]
ldc %a, paddleLeftY
stb %a, %b
ldc %a, paddleRightY
stb %a, %b




mainLoop:
@call updateInputs, $0
@call updateGame,   $0
@call updateScreen, $0

@ldc %a, mainLoop
jump %a




# --------------------------------------
resetBall:
@function $0

ldc %a, ballColor
ldc %b, colorWhite
stb %a, %b

ldc %a, ballX
ldc %b, [screenWidth / 2]
stb %a, %b

ldc %a, ballY
ldc %b, [screenHeight - 10] #[screenHeight / 2] TODO temporary dev thing
stb %a, %b

# use RNG to determine paddle direction.
# X speed = bit 0
# Y speed = bit 1
# 0 => speed = -1
# 1 => speed = 1
@ldc %c, devrng
ldb %c, %c


# (highZ & 0b01) * 2 - 1:
#   bit0(highZ) == 0 => -1
#   bit0(highZ) == 1 =>  1
ldc %d, $1
ldc %b, $0b01
and %a, %c, %b

ldc %b, $2
mul %a, %a, %b

sub %a, %a, %d

ldc %b, ballSpeedX
stb %b, %a


# same for ballSpeedY (but use bit 1 instead of bit 0 - so need to shr it as well)
ldc %b, $0b10
and %a, %c, %b
shr %a, %a, %d

ldc %b, $2
mul %a, %a, %b

sub %a, %a, %d

@ldc %a, $1 # TODO temporary dev thing
ldc %b, ballSpeedY
stb %b, %a

@return




# --------------------------------------
# read button device and set inputLeftDir and inputRightDir.
# -1 => paddle moving UP (Y--)
#  0 => paddle staying still
#  1 => paddle moving DOWN (Y++)
# args: none
updateInputs:
@function $0

# TODO we are using buttons to move the paddles up and down. What do you think of using a rotary encoder to determine its position? Like one of those truly old pong machines. It might even be simpler to code.


# *inputLeftDir = readButton(state, buttonLeftDown) - readButton(state, buttonLeftUp)
# so that pushing down makes it 1, pushing up makes it -1, and pushing both or none makes it 0.
@push_const buttonLeftDown
@call_value readButton, $1

@push_const buttonLeftUp
@call_value readButton, $1

@sub
@pop_mem_v inputLeftDir

# same but for right
@push_const buttonRightDown
@call_value readButton, $1

@push_const buttonRightUp
@call_value readButton, $1

@sub
@pop_mem_v inputRightDir


@return




# --------------------------------------
# args: mask
# returns: 0x0 or 0x1
readButton:
@function $0

# return ((state & mask) > 0) & 1
# where something > something returns 0x0000 or 0xFFFF

@push_mem devbutton
@push_arg $0
@and
@push_const $0
@gt
@push_const $1
@and

@return_value




# --------------------------------------
# args: none
updateGame:
@function $0

# Move paddles according to inputLeftDir and inputRightDir
# limit paddle Y coordinates to [0, screenHeight - paddleHeight]

# *paddleLeftY = clamp(*paddleLeftY + (*inputLeftDir * paddleSpeed), 0, screenHeight - paddleHeight)
@push_mem paddleLeftY
@push_mem inputLeftDir
@push_const paddleSpeed
@mul
@add
@push_const $0
@max
@push_const [screenHeight - paddleHeight]
@min
@pop_mem_v paddleLeftY

@push_mem paddleRightY
@push_mem inputRightDir
@push_const paddleSpeed
@mul
@add
@push_const $0
@max
@push_const [screenHeight - paddleHeight]
@min
@pop_mem_v paddleRightY


# move ball according to ballSpeedX and ballSpeedY
@push_mem ballX
@push_mem ballSpeedX
@add
@pop_mem_v ballX

@push_mem ballY
@push_mem ballSpeedY
@add
@pop_mem_v ballY




# collide ball with paddles. On collision, invert X speed and set ballColor to the color of the paddle it hit.
# if ballX == paddleLeftX && ballY >= paddleLeftY && ballY <= (paddleLeftY + paddleHeight):
#   ballX = paddleLeftX + 2
#   ballSpeedX = 0 - ballSpeedX
#   ballColor = colorPaddleLeft
@push_mem ballX
@push_const paddleLeftX
@eq

@push_mem ballY
@push_mem paddleLeftY
@gte

@push_mem ballY
@push_mem paddleLeftY
@push_const paddleHeight
@add
@lte

@and
@and
@if updateGame_flipBallXLeft
    @push_const [paddleLeftX + 2]
    @pop_mem_v ballX
    
    @push_const $0
    @push_mem ballSpeedX
    @sub
    @pop_mem_v ballSpeedX
    
    @push_const colorPaddleLeft
    @pop_mem_v ballColor
    
    # TODO play sound
updateGame_flipBallXLeft:


# same for right
@push_mem ballX
@push_const paddleRightX
@eq

@push_mem ballY
@push_mem paddleRightY
@gte

@push_mem ballY
@push_mem paddleRightY
@push_const paddleHeight
@add
@lte

@and
@and
@if updateGame_flipBallXRight
    @push_const [paddleRightX - 2]
    @pop_mem_v ballX
    
    @push_const $0
    @push_mem ballSpeedX
    @sub
    @pop_mem_v ballSpeedX
    
    @push_const colorPaddleRight
    @pop_mem_v ballColor
    
    # TODO play sound
updateGame_flipBallXRight:






# collide ball with top/bottom walls. Invert Y speed.
# if ballY <= 0:
#   ballY = 0
#   ballSpeedY = 0 - ballSpeedY
@push_mem ballY
@push_const $0
@lte
@if updateGame_flipBallYTop
    @push_const $0
    @pop_mem_v ballY

    @push_const $0
    @push_mem ballSpeedY
    @sub
    @pop_mem_v ballSpeedY
    
    # TODO play sound
updateGame_flipBallYTop:

# if ballY >= screenHeight:
#   ballY = screenHeight - 1
#   ballSpeedY = 0 - ballSpeedY
@push_mem ballY
@push_const screenHeight
@gte
@if updateGame_flipBallYBottom
    @push_const [screenHeight - 2]
    @pop_mem_v ballY

    @push_const $0
    @push_mem ballSpeedY
    @sub
    @pop_mem_v ballSpeedY
    
    # TODO play sound
updateGame_flipBallYBottom:


# collide ball with left/right walls. Call resetBall, and increment score.
@push_mem ballX
@push_const $0
@lte
@if updateGame_ballWallLeft
    @push_mem scoreRight
    @push_const $1
    @add
    @pop_mem_v scoreRight
    
    @call resetBall, $0
    
    # TODO play sound
updateGame_ballWallLeft:


@push_mem ballX
@push_const screenWidth
@gte
@if updateGame_ballWallRight
    @push_mem scoreLeft
    @push_const $1
    @add
    @pop_mem_v scoreLeft
    
    @call resetBall, $0
    
    # TODO play sound
updateGame_ballWallRight:



# WISHLIST FEATURE: need a clock device that we can use the move objects at a consistent speed.
# It doesn't necessarily have to count seconds "outside the simulator", but it at least needs to count clock ticks independently of the CPU.
# But until this is implemented, we can get away with tying the object speed to the clock speed, as the program's main loop *should* run in constant time, so we can just set the clock speed to directly determine the object speed.

@return




# --------------------------------------
# args: none
updateScreen:
@function $0

# TODO rather than clearing the entire screen every frame (which is very slow), only clear the parts of the screen that we're going to "undraw".
# ie clear the position of the ball, update the ball, draw the ball again.
# maybe write a "updateScreenPreGame" function that does this for every object.
# the score and midline don't need to be cleared as they don't move.
@call clearScreen, $0

@push_const paddleRightX
@push_mem   paddleRightY
@push_const colorPaddleRight
@call drawPaddle, $3

@push_const paddleLeftX
@push_mem   paddleLeftY
@push_const colorPaddleLeft
@call drawPaddle, $3


# draw score
# bug: overread when score > 9
# this probably doesn't result in any memory corruption or incorrect behaviour.
@push_const scoreLeftX       # x pos
@push_const scoreY           # y pos
@push_mem   scoreLeft        # character index
@push_const colorPaddleLeft  # color
@call drawCharacter, $4

@push_const scoreRightX      # x pos
@push_const scoreY           # y pos
@push_mem   scoreRight       # character index
@push_const colorPaddleRight # color
@call drawCharacter, $4


@call drawMidline, $0


@push_mem ballX
@push_mem ballY
@push_mem ballColor
@call drawBall, $3


# Flip frame buffer
@ldc %a, devframe
ldb %a, %b
not %b, %b

ldc %c, $1
and %b, %b, %c
stb %a, %b


@return




# --------------------------------------
clearScreen:
@function $0

# %c = &frameBuffer
# displayEnd = %c + displaySize
# do {
#   *c = colorBackground;
#   %c = %a + 1;
# } while (%c <= displayEnd);

@call_value getFrameBuffer, $0
@pop_reg %c

# %b = displayEnd = %c + displaySize
@ldc %b, displaySize
add %d, %c, %b


ldc %a, colorBackground
# %b will be used for both $1 and the jump label

clearScreen_loop1:

stb %c, %a

ldc %b, $1
add %c, %c, %b

ldc %b, clearScreen_loop1
jle %b, %c, %d

@return




# --------------------------------------
# args: X coordinate, Y coordinate of paddle top, color
drawPaddle:
@function $0

@push_arg $0
@push_arg $1
@push_const paddleHeight
@push_arg $2
@call drawVerticalLine, $4

@return




# --------------------------------------
# args: X coordinate, Y coordinate of top, length in pixels, color
drawVerticalLine:
@function $2

# pixel* position = &frameBuffer + y * screenWidth + x
@call_value getFrameBuffer, $0

@push_arg $1
@push_const screenWidth
@mul
@add
@push_arg $0
@add
@pop_local $0

# endPosition = position + length * screenWidth
@push_local $0
@push_arg $2
@push_const screenWidth
@mul
@add
@pop_local $1

# do {
#   *position = color;
#   position = position + screenWidth;
# } while (position < endPosition);

# position    = local 0
# endPosition = local 1
# color       = arg 2
# screenWidth = constant

drawVerticalLine_loop1:
@push_arg $3
@push_local $0
@pop_mem_av

@push_local $0
@push_const screenWidth
@add
@pop_local $0

@push_local $0
@push_local $1

@pop_reg %d # %d = endPosition
@pop_reg %c # %c = position
@ldc %b, drawVerticalLine_loop1
jlt %b, %c, %d

@return




# --------------------------------------
# args: X coordinate, Y coordinate, color
drawBall:
@function $0

# pixel = &frameBuffer + screenWidth * Y + X
@call_value getFrameBuffer, $0
@push_const screenWidth
@push_arg $1
@mul
@add
@push_arg $0
@add

# *pixel = color
@push_arg $2
@pop_mem_va

@return




# --------------------------------------
# args: none
# returns: current framebuffer pointer
getFrameBuffer:
@function $0
# display + (*devframe & 1) * displaySize
@push_const display
@push_mem devframe
@not
@push_const $1
@and
@push_const displaySize
@mul
@add

@return_value




# --------------------------------------
# args: x position, y position, character index, color
.const drawCharacter_arg_xPos,      $0
.const drawCharacter_arg_yPos,      $1
.const drawCharacter_arg_charIndex, $2
.const drawCharacter_arg_color,     $3

# locals: character bitmap, pixel pointer, y loop, x loop
.const drawCharacter_loc_charBmp,   $0
.const drawCharacter_loc_pixelPtr,  $1
.const drawCharacter_loc_yLoop,     $2
.const drawCharacter_loc_xLoop,     $3
drawCharacter:
@function $4
# int characterFont = font[characterIndex];
# color* pixel = &frameBuffer + screenWidth * yPos + xPos
# for (int y = 0; y <= fontHeight; y++) {
#   for (int x = 0; x <= fontWidth; x++) {
#     pixelColor = characterFont & 1 > 0 ? color : colorBackground;
#     setPixel(x, y, pixelColor);
#     characterFont = characterFont >> 1;
#     pixel = pixel + 1
#   }
#   pixel = pixel + (screenWidth - fontWidth)
# }

# characterFont
@push_const fontArray
@push_arg drawCharacter_arg_charIndex
@add
@push_mem_a
@pop_local drawCharacter_loc_charBmp

# pixel pointer
@call_value getFrameBuffer, $0
@push_const screenWidth
@push_arg drawCharacter_arg_yPos
@mul
@add
@push_arg drawCharacter_arg_xPos
@add
@pop_local drawCharacter_loc_pixelPtr

# loop variables
@push_const $0
@pop_local drawCharacter_loc_yLoop

drawCharacter_yLoop:
    @push_const $0
    @pop_local drawCharacter_loc_xLoop
    drawCharacter_xLoop:
        # pixelColor
        @push_local drawCharacter_loc_charBmp
        @push_const $1
        @and
        @push_const $0
        @gt
        @if drawCharacter_pushBackground
        drawCharacter_pushForeground:
            @push_arg drawCharacter_arg_color
            @ldc %b, drawCharacter_endif1
            jump %b
        drawCharacter_pushBackground:
            @push_const colorBackground
        drawCharacter_endif1:

        # setPixel
        @push_local drawCharacter_loc_pixelPtr
        @pop_mem_av

        # shift bitmap
        @push_local drawCharacter_loc_charBmp
        @shr $1 # TODO shr, shr_n, shl, shl_n macros
        @pop_local drawCharacter_loc_charBmp

        # pixel = pixel + 1
        @push_local drawCharacter_loc_pixelPtr
        @push_const $1
        @add
        @pop_local drawCharacter_loc_pixelPtr


        # x++
        @push_local drawCharacter_loc_xLoop
        @push_const $1
        @add
        @pop_local drawCharacter_loc_xLoop

        # jump to xLoop if x < fontWidth
        @push_local drawCharacter_loc_xLoop
        @pop_reg %c
        @ldc %a, drawCharacter_xLoop
        ldc %b, fontWidth
        jlt %a, %c, %b

    # pixel += screenWidth - fontWidth - 1
    # - 1 because we already incremented pixel last xLoop
    @push_local drawCharacter_loc_pixelPtr
    @push_const [screenWidth - fontWidth]
    @add
    @pop_local drawCharacter_loc_pixelPtr
    
    # y++
    @push_local drawCharacter_loc_yLoop
    @push_const $1
    @add
    @pop_local drawCharacter_loc_yLoop

    # jump to yLoop if y < fontHeight
    @push_local drawCharacter_loc_yLoop
    @pop_reg %c
    @ldc %a, drawCharacter_yLoop
    ldc %b, fontHeight
    jlt %a, %c, %b

@return




# --------------------------------------
drawMidline:
.const drawMidline_loc_pixel,     $0
.const drawMidline_loc_lastPixel, $1
.const drawMidline_loc_y,         $2
@function $3
# color* pixel = &frameBuffer + screenWidth / 2
# color* lastPixel = pixel + displaySize
# int y = 0
# do {
#   *pixel = y & 0b100 == 0 ? colorBackground : colorMidline
#   pixel += screenWidth
#   y++
# } while (pixel < lastPixel)

@call_value getFrameBuffer, $0
@push_const [screenWidth / 2]
@add
@pop_local drawMidline_loc_pixel


@push_local drawMidline_loc_pixel
@push_const displaySize
@add
@pop_local drawMidline_loc_lastPixel


@push_const $2
@pop_local drawMidline_loc_y


drawMidline_startLoop:
    @push_local drawMidline_loc_y
    @push_const $0b100
    @and
    @push_const $0
    @eq
    @if drawMidline_pushColorMidline
        @push_const colorBackground
        @ldc %a, drawMidline_endif1
        jump %a
    drawMidline_pushColorMidline:
        @push_const colorMidline
    drawMidline_endif1:
    
    @push_local drawMidline_loc_pixel
    @pop_mem_av
    
    @push_local drawMidline_loc_pixel
    @push_const screenWidth
    @add
    @pop_local drawMidline_loc_pixel
    
    @push_local drawMidline_loc_y
    @push_const $1
    @add
    @pop_local drawMidline_loc_y

    @push_local drawMidline_loc_pixel
    @push_local drawMidline_loc_lastPixel
    @gte
    @if drawMidline_startLoop

@return




# --------------------------------------
fontArrayInProgramMemory:
# Font is 3 pixels wide and 5 pixels tall.
# Every group of 3 bits, right to left, is a row, which are drawn top to bottom.
# Bits in a group are drawn LSB left, MSB right.
.words $0b111_101_101_101_111 # digit 0
.words $0b111_010_010_011_010 # digit 1
.words $0b111_001_111_100_111 # digit 2
.words $0b111_100_111_100_111 # digit 3
.words $0b100_100_111_101_101 # digit 4
.words $0b111_100_111_001_111 # digit 5
.words $0b111_101_111_001_111 # digit 6
.words $0b100_100_100_100_111 # digit 7
.words $0b111_101_111_101_111 # digit 8
.words $0b111_100_111_101_111 # digit 9
